# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/dotfiles

# Load functions for distinguishing machines
fpath=($DOTFILES/funcs $fpath)

# Source oh-my-zsh
plugins=(git npm python sudo golang docker docker-compose)
export ZSH="$HOME/.oh-my-zsh"
source $ZSH/oh-my-zsh.sh
unalias -m '*'

# Use PROMPT_SUBST
setopt PROMPT_SUBST

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path files
for file in ${${config_files:#*/path.zsh}}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

unset config_files
